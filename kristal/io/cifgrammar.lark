cif : _NEWLINE* datablock*
datablock : datablock_heading (loop | entry) + 
datablock_heading : "data_"i NON_BLANK_STRING _NEWLINE
loop : loop_header loop_body
loop_header : "loop_" _NEWLINE (tag _NEWLINE)+
loop_body : (_value _WS)+
tag : "_" NON_BLANK_STRING
_value : _number | string
entry : tag _WS _value _NEWLINE
_number : ( integer | float) _UNCERTAINITY?
integer.3 : INTEGER
float.2 : FLOAT
string : _unquoted_string | _multiline_string | _quoted_string
_unquoted_string.1 : UNQUOTED_STRING
_multiline_string : ";"  MULTILINE_STRING ";"
_quoted_string : single_quoted_string | double_quoted_string
single_quoted_string : "'" SINGLE_QUOTED_STRING_INNER "'"
double_quoted_string : "\"" DOUBLE_QUOTED_STRING_INNER"\""
UNQUOTED_STRING : LEAD_CHAR NON_BLANK_CHAR*
DOUBLE_QUOTED_STRING_INNER : (NON_BLANK_NON_QUOTE_CHAR | "'" | /\"[^ \r\n\t]/ | " " | "\t")+ 
SINGLE_QUOTED_STRING_INNER : (NON_BLANK_NON_QUOTE_CHAR | "\"" | /'[^ \r\n\t]/ | " " | "\t")+
MULTILINE_STRING : (NEWLINE | (PRINTABLE_STRING NEWLINE))(LEAD_CHAR PRINTABLE_STRING NEWLINE)*
PRINTABLE_CHAR : NON_BLANK_CHAR | " " | "\t"
NON_BLANK_CHAR : NON_BLANK_NON_QUOTE_CHAR | "\"" | "'"
NON_BLANK_NON_QUOTE_CHAR : ORDINARY_CHAR | "#" | "$" | "_" |";" | "^" | "[" | "]"
ORDINARY_CHAR : LETTER | DIGIT | "!" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | "<" | "=" | ">" | "?" | "@" | "\\" | "\^" | "`"  | "{" | "|" | "}" | "~"
PRINTABLE_STRING : PRINTABLE_CHAR+
NON_BLANK_STRING : NON_BLANK_CHAR+
LEAD_CHAR : ORDINARY_CHAR | "\"" | "#" | "$" | "'" | " " | "\t" | "[" | "]"
BASE_STRING : ORDINARY_CHAR+
FLOAT : ( INTEGER EXPONENT ) | ( ( "+" | "-" )? (( DIGIT* "." UNSIGNEDINTEGER ) | ( DIGIT+ "." )) EXPONENT? )
EXPONENT : ("e"| "E") ( "+" | "-" )? UNSIGNEDINTEGER
INTEGER : ( "+" | "-" )? UNSIGNEDINTEGER
_UNCERTAINITY : "(" UNSIGNEDINTEGER ")"
UNSIGNEDINTEGER : DIGIT+

COMMENT: /#[^\r\n]*/
NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+
_WS : WS
WS : (" " | "\t" | _NEWLINE )+
NAME: /[a-zA-Z_]\w*/
%import common.LETTER
%import common.DIGIT
%ignore COMMENT